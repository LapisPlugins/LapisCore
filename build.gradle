/*
 * Copyright 2021 Benjamin Martin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'io.codearte.nexus-staging' version '0.30.0'
}

group = 'net.lapismc'
archivesBaseName = "LapisCore"
version = '1.11.0'
description = 'LapisCore'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }
    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.tchristofferson:ConfigUpdater:2.0-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.0'
    compileOnly 'net.luckperms:api:5.4'
}

shadowJar {
    relocate('com.google.code', 'net.lapismc.google.code')
}

javadoc {
    exclude 'net/lapismc/lapiscore/compatibility'
    exclude 'net/lapismc/lapiscore/placeholder'
    exclude 'net/lapismc/lapiscore/utils/LapisItemBuilder.java'
    exclude 'net/lapismc/lapiscore/utils/Metrics.java'
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

nexusStaging {
    packageGroup = "net.lapismc" //optional if packageGroup == project.getGroup()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'LapisCore'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The core library for my plugins'
                url 'https://github.com/LapisPlugins/LapisCore'

                scm {
                    connection 'git@github.com:LapisPlugins/LapisCore.git'
                    developerConnection 'git@github.com:LapisPlugins/LapisCore.git'
                    url 'https://github.com/LapisPlugins/LapisCore'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Dart2112'
                        name 'Benjamin Martin'
                        email 'dart2112@lapismc.net'
                    }
                }
            }
        }
    }
}

