/*
 * Copyright 2025 Benjamin Martin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id "com.gradleup.shadow" version "9.0.0-rc1"
}

group = 'net.lapismc'
archivesBaseName = "LapisCore"
version = '1.13.9'
description = 'LapisCore'
sourceCompatibility = '21'
targetCompatibility = '21'
defaultTasks 'clean', 'build', 'shadow'

repositories {
    mavenCentral()
    //Paper Repo
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    //Personal Repo
    maven {
        name = "lapismc-repo"
        url = "https://maven.lapismc.net/repository/maven/"
    }
    //DiscordSRV Repo
    maven {
        url 'https://nexus.scarsz.me/content/groups/public/'
    }
    //PAPI Repo
    maven {
        url = uri('https://repo.helpch.at/releases/')
    }
}

dependencies {
    implementation 'com.tchristofferson:ConfigUpdater:2.2'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.9.Final'
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.discordsrv:discordsrv:1.30.2'
}

javadoc {
    exclude 'net/lapismc/lapiscore/compatibility'
    exclude 'net/lapismc/lapiscore/placeholder'
    exclude 'net/lapismc/lapiscore/utils/LapisItemBuilder.java'
    exclude 'net/lapismc/lapiscore/utils/Metrics.java'
}

shadowJar {
    archiveFileName.set(archivesBaseName + "-" + project.version + ".jar")
    exclude 'javax/**'
    relocate("org.ocpsoft.prettytime", "net.lapismc.lapiscore.utils.prettytime")
    relocate("com.tchristofferson.configupdater", "net.lapismc.lapiscore.utils.configupdater")
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name 'nexus'
            url "https://nexus.dnablue2112.com/repository/maven-releases/"
            credentials {
                username repoUser
                password repoPassword
            }
        }
    }
}

